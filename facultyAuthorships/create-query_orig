PREFIX core: <http://vivoweb.org/ontology/core#>
PREFIX fn: <http://www.w3.org/2005/xpath-functions#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX cub: <https://experts.colorado.edu/individual/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
 ?auth rdf:type core:Authorship .
 ?auth <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#mostSpecificType> core:Authorship .
 ?auth core:relates ?pub .
 ?pub core:relatedBy ?auth .
 ?auth core:relates ?pers .
 ?pers core:relatedBy ?auth .
 ?pub rdf:type bibo:Document .
 ?pub rdfs:label ?title .
 ?pub rdf:type ?type .
 ?pub <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#mostSpecificType> ?type .
 ?pub a <http://purl.obolibrary.org/obo/IAO_0000030> .
 ?pub core:dateTimeValue ?pubdate .
 ?pubdate a core:DateTimeValue .
 ?pubdate <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#mostSpecificType> core:dateTimeValue .
 ?pubdate core:dateTimePrecision core:yearPrecision .
 ?pubdate core:dateTime ?date.
}
WHERE  {
 ?work cub:ws_fw_FIS_ID ?person .
 ?work cub:ws_fw_ELEMENTS_PUB_ID ?pub_id .
 ?work cub:ws_fw_ELEMENTS_PUB_TITLE ?title .
 bind(IRI(CONCAT('https://experts.colorado.edu/individual/fisid_', ?person)) as ?pers)
 bind(IRI(CONCAT('https://experts.colorado.edu/individual/pubid_', ?pub_id)) as ?pub)
 bind(IRI(CONCAT('https://experts.colorado.edu/individual/authorship_', ?person, ?pub_id)) as ?auth)
 OPTIONAL {
          ?work cub:ws_fw_ELEMENTS_DERIVED_PUB_DATE ?d.
          bind(IRI(CONCAT('https://experts.colorado.edu/individual/pubdate_',?pub_id)) as ?pubdate)
          bind(STR(CONCAT(?d, 'T00:00:00')) as ?dt)
          bind(STRDT(?dt, xsd:dateTime) as ?date)
          }
 
 OPTIONAL { ?work cub:ws_fw_ELEMENTS_PUB_TYPE "journal-article" .
            BIND(URI(bibo:Journal) AS ?type)
          }
 OPTIONAL { ?work cub:ws_fw_ELEMENTS_PUB_TYPE "book" .
            BIND(URI(bibo:Book) AS ?type)
          }
 OPTIONAL { ?work cub:ws_fw_ELEMENTS_PUB_TYPE "chapter" .
            BIND(URI(bibo:Chapter) AS ?type)
          }
 OPTIONAL { ?work cub:ws_fw_ELEMENTS_PUB_TYPE "conference-paper" .
            BIND(URI(bibo:ConferencePaper) AS ?type)
          }
}
