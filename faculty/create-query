PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX owl:   <http://www.w3.org/2002/07/owl#> 
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#> 
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#> 
PREFIX swrl:  <http://www.w3.org/2003/11/swrl#> 
PREFIX swrlb: <http://www.w3.org/2003/11/swrlb#> 
PREFIX bibo: <http://purl.org/ontology/bibo/> 
PREFIX dcelem: <http://purl.org/dc/elements/1.1/> 
PREFIX dcterms: <http://purl.org/dc/terms/> 
PREFIX event: <http://purl.org/NET/c4dm/event.owl#> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX geo: <http://aims.fao.org/aos/geopolitical.owl#> 
PREFIX pvs: <http://vivoweb.org/ontology/provenance-support#> 
PREFIX ero: <http://purl.obolibrary.org/obo/> 
PREFIX scires: <http://vivoweb.org/ontology/scientific-research#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX j.1: <http://vitro.mannlib.cornell.edu/ns/vitro/public#> 
PREFIX core: <http://vivoweb.org/ontology/core#> 
PREFIX vlocal: <https://experts.colorado.edu/ontology/vivo-fis#>  
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX arg: <http://purl.obolibrary.org/obo/>
PREFIX cub: <https://experts.colorado.edu/individual/>

Construct {  ?person <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> <http://xmlns.com/foaf/0.1/Person> .
 ?person arg:ARG_2000028 ?vcard .
 ?person <http://www.w3.org/2000/01/rdf-schema#label> ?label .
 ?vcard vcard:hasEmail ?vcard_email.
 ?vcard rdf:type <http://purl.obolibrary.org/obo/ARG_2000379> .
 ?vcard rdf:type vcard:Kind .
 ?vcard vitro:mostSpecificType <http://purl.obolibrary.org/obo/ARG_2000379> .
 ?vcard <http://purl.obolibrary.org/obo/ARG_2000029> ?person .
 ?vcard_email vcard:email ?e.
 ?vcard_email rdf:type vcard:Email .
 ?vcard_email vitro:mostSpecificType vcard:Email .
 ?vcard_email rdf:type vcard:Communication .
 ?vcard vcard:hasName ?vcard_name .
 ?vcard_name rdf:type vcard:Name .
 ?vcard_name vcard:givenName ?firstName .
 ?vcard_name vcard:middleName ?middleName .
 ?vcard_name vcard:familyName ?lastName .
 ?vcard vcard:hasTitle ?vcard_title .
 ?vcard_title vcard:title ?moniker .
 ?vcard_title rdf:type vcard:Title .
 ?vcard_title rdf:type vcard:Organizational .
 ?vcard_title rdf:type <http://www.w3.org/2002/07/owl#Thing> .
 ?vcard_title rdf:type vcard:Addressing .
 ?vcard_title rdf:type <http://www.w3.org/2006/vcard/ns#Communication> .
 ?vcard_title rdf:type <http://www.w3.org/2006/vcard/ns#Identification> .
 ?vcard_title vitro:mostSpecificType vcard:Title .
 ?person <https://experts.colorado.edu/ontology/vivo-fis#fisId> ?fisId .
 ?person <https://experts.colorado.edu/ontology/vivo-fis#uuid> ?uuid .
 ?person <http://vivoweb.org/ontology/core#freetextKeyword> ?keywords .
 ?person core:orcidId ?orcid .
 ?orcid rdf:type <http://www.w3.org/2002/07/owl#Thing> .
 ?orcid vitro:mostSpecificType <http://www.w3.org/2002/07/owl#Thing> .
 }
Where {
 ?person cub:ws_fm_LABEL ?label .
 optional { ?person cub:ws_fm_FIRSTNAME ?firstName . }
 optional { ?person cub:ws_fm_MIDDLENAME ?middleName . }
 ?person cub:ws_fm_LASTNAME ?lastName .
 optional { ?person cub:ws_fm_MONIKER ?moniker . }
 optional { ?person cub:ws_fm_EMAIL ?email . }
 optional { ?person cub:ws_fm_KEYWORDS ?keywords . }
 ?person cub:ws_fm_FISID ?fisId .
 ?person cub:ws_fm_UUID ?uuid .
 OPTIONAL { ?person cub:ws_fm_ORCIDID ?orc .
            BIND(URI(?orc) AS ?orcid) }
 BIND(STRDT(?email, xsd:string) AS ?e)
 BIND(URI(CONCAT('https://experts.colorado.edu/individual/vcard_', ?fisId)) AS ?vcard)
 BIND(URI(CONCAT('https://experts.colorado.edu/individual/vcard_email_',?fisId)) AS ?vcard_email)
 BIND(URI(CONCAT('https://experts.colorado.edu/individual/vcard_name_',?lastName,'.',?firstName,'_',?fisId)) AS ?vcard_name)
 BIND(URI(CONCAT('https://experts.colorado.edu/individual/vcard_title_',?fisId)) AS ?vcard_title) }
